# shellcheck shell=bash

SCRIPT_DIR="$(cd "$(dirname "$0")" && pwd)"
source "$SCRIPT_DIR/../common/realpath.sh"
DOTFILES_DIR="$(resolve_realpath "$SCRIPT_DIR/../../")"
unset SCRIPT_DIR

# Zsh options ----------------------------------------------

setopt ALWAYS_TO_END           # Move cursor to end after completion
setopt APPEND_HISTORY          # Append rather than overwrite history file
setopt AUTO_CD
setopt COMPLETE_IN_WORD        # Allow completion in middle of words
setopt CORRECT                 # Command correction suggestions
setopt NO_BEEP
setopt NO_CASE_GLOB
setopt NO_CLOBBER
setopt PROMPT_SUBST            # Expand PROMPT and RPROMPT

autoload -Uz colors && colors  # Enable ZSH color names
autoload -Uz vcs_info          # Enable ZSH VCS info

# Common setup ---------------------------------------------

source "$DOTFILES_DIR/shell/common/path.sh"
source "$DOTFILES_DIR/shell/common/exports.sh"
source "$DOTFILES_DIR/shell/common/aliases.sh"
source "$DOTFILES_DIR/shell/common/functions.sh"

# Completion ---------------------------------------------------------

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'  # No case
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}" # Colors
zstyle ':completion:*' menu select                      # Menu
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $HOME/.zsh/cache

# Homebrew completions
if [[ -n $HOMEBREW_PREFIX ]]; then
    if [[ -d $HOMEBREW_PREFIX/share/zsh-completions ]]; then
        fpath+="$HOMEBREW_PREFIX/share/zsh-completions"
    fi
    if [[ -d $HOMEBREW_PREFIX/share/zsh/site-functions ]]; then
        fpath+="$HOMEBREW_PREFIX/share/zsh/site-functions"
    fi

    # Hack for work setup: I have local copies of completions
    # and need to ignore the Homebrew ones
    if [[ -n $TRS_HOMEBREW_DROP_ZSH_FPATH ]]; then
        fpath=(${fpath:#/opt/homebrew/share/zsh/site-functions})
        fpath=(${fpath:#/opt/homebrew/share/zsh})
    fi
fi

# Local completions
for d in ~/.local/share/zsh/site-functions ~/.zfunc; do
  [[ -d $d ]] && fpath=($d $fpath)
done

autoload -Uz compinit  # Completion
autoload -Uz bashcompinit  # Incorporate Bash, too

# Optimization: Don't rebuild the completion dump every time we start
typeset -g ZSH_COMPDUMP="${ZDOTDIR:-$HOME}/.zcompdump-$HOST-$ZSH_VERSION"

today_=$(date +'%j')
cache_=""
if zmodload -F zsh/stat b:zstat 2>/dev/null && zmodload -F zsh/datetime b:strftime 2>/dev/null; then
  today_=$(strftime %j $EPOCHSECONDS)
  if [[ -s $ZSH_COMPDUMP ]] && zstat -A zstat_mtime +mtime -- "$ZSH_COMPDUMP" 2>/dev/null; then
    cache_=$(strftime %j ${zstat_mtime[1]})
  fi
fi

if [[ -n $cache_ && $today_ == $cache_ ]]; then
  compinit -d "$ZSH_COMPDUMP"
else
  compinit -C -d "$ZSH_COMPDUMP"
fi

bashcompinit

unset zstat_mtime

# Compile the dump for faster reads
if [[ -s $ZSH_COMPDUMP && ( ! -s $ZSH_COMPDUMP.zwc || $ZSH_COMPDUMP -nt $ZSH_COMPDUMP.zwc ) ]]; then
  zcompile -R -- "$ZSH_COMPDUMP" "$ZSH_COMPDUMP.zwc"
fi


# Common tools ---------------------------------------------

source "$DOTFILES_DIR/shell/common/tools.sh"

# Keybindings --------------------------------------------------------

# Emacs bindings
bindkey -e

# macOS-like customizations
bindkey "^A" beginning-of-line       # Ctrl+A
bindkey "^E" end-of-line             # Ctrl+E
bindkey "^[[1;3D" backward-word      # Option+Left
bindkey "^[[1;3C" forward-word       # Option+Right
bindkey "^[[1;9D" beginning-of-line  # Cmd+Left
bindkey "^[[1;9C" end-of-line        # Cmd+Right
bindkey "^[[3~" delete-char          # Delete
bindkey "^H" backward-delete-char    # Backspace
bindkey "^[[3;3~" backward-kill-word # Option+Backspace
bindkey "^[[3;9~" kill-word          # Option+Delete
bindkey "^U" backward-kill-line      # Cmd+Backspace
bindkey "^K" kill-line               # Cmd+Delete

# Arrow key history (with prefix matching)
autoload -U up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search    # Up arrow
bindkey "^[[B" down-line-or-beginning-search  # Down arrow

# Split on "/" so we can option+backspace in directories
WORDCHARS=${WORDCHARS//\/}

# History configuration ----------------------------------------------

setopt EXTENDED_HISTORY         # Save timestamp and duration
setopt HIST_EXPIRE_DUPS_FIRST   # Expire duplicates first when trimming history
setopt HIST_IGNORE_ALL_DUPS     # Remove older duplicate when new one added
setopt HIST_IGNORE_SPACE        # Don't save commands starting with space
setopt HIST_REDUCE_BLANKS       # Remove extra whitespace before saving
setopt HIST_SAVE_NO_DUPS        # Don't write duplicate commands
setopt HIST_VERIFY              # Show expanded history command before executing
setopt INC_APPEND_HISTORY_TIME  # Write commands immediately w/timestamps
setopt HIST_FCNTL_LOCK          # Lock history file when possible
setopt SHARE_HISTORY            # Share history between all zsh sessions

HISTFILE="$HOME/.zsh_history"
HISTSIZE=100000  # 100,000 commands in memory
SAVEHIST=500000  # 500,000 commands on disk

touch "$HISTFILE" 2>/dev/null
chmod 600 "$HISTFILE" 2>/dev/null

# Prompt ---------------------------------------------------

# Configure VCS display
zstyle ':vcs_info:*' enable                 # Enable for everything
zstyle ':vcs_info:*' check-for-changes true # Allow checking for repo changes
zstyle ':vcs_info:*' stagedstr "+"          # Plus sign if any staged changes
zstyle ':vcs_info:*' unstagedstr "*"        # Asterisk if any unstaged changes
zstyle ':vcs_info:*' formats "%{$fg[magenta]%}%b%u%c%{$reset_color%}"

precmd() {
    local exit_status=$?
    vcs_info
    if [[ $exit_status -ne 0 && $exit_status -ne 130 ]]; then
        TRS_PS1_STATUS_COLOR="%{$fg[yellow]%}"
    else
        TRS_PS1_STATUS_COLOR=""
    fi
}
PROMPT=$'${TRS_PS1_LABEL:+"$TRS_PS1_LABEL "}%{$fg[blue]%}%${TRS_PS1_NUM_DIRS:-1}d%{$reset_color%} ${TRS_PS1_STATUS_COLOR}${TRS_PS1_ICON:-%(!.#.$)}%{$reset_color%} '
RPROMPT='$vcs_info_msg_0_'

# Local overrides ---------------------------------------------------

if [[ -z ${DOTFILES_LOCAL_LOADED-} ]]; then
    DOTFILES_LOCAL_LOADED=1
    dotfiles_local="${XDG_CONFIG_HOME:-$HOME/.config}/dotfiles/local.sh"
    [[ -r $dotfiles_local ]] && source "$dotfiles_local"
    unset dotfiles_local
fi
